
#
# Copyright (c) 2016 acefael
#
_package sw
$

_pragma(classify_level=advanced, topic={GEOJSON})
_method rwo_record.as_google_geojson( _optional out_stream )
	## serialises _self in geojson format onto out_stream.
	##
	## out_stream defaults to !output!

	_local out << out_stream.default(!output!)

	_constant COMMA << %,

	_local astr << _proc(str) >> write_string( %" , str , %" ) _endproc
	_local wany << _proc(_gather any)
			       _import out
			       _for elm _over any.elements()
			       _loop
				       out.write(elm)
			       _endloop
		       _endproc

	_local not_first? << _false

	#feature
	wany( %{,astr("type"),%:,astr("Feature"),COMMA)
	#id - urn
	wany( astr("id"),%:,astr(urn_manager.get_urn_from_object(_self)), COMMA )
	#geom - only get default geometry. skip if geometry is unset.
	_if (a_geom << _self.default_geometry) _isnt _unset 
	_then
		wany(astr("geometry"),":")
		a_geom.as_google_geojson(out)
	_else
		write(_self.write_string + " default geometry is unset.")
	_endif
	
	#properties
	wany( COMMA,astr("properties"),%:,%{)
	_for f _over _self.visible_fields().elements()
	_loop
		# TODO what to do with join fields
		_if f.is_join?
		_then
			_continue _endif
		# geometry handled further down
		_if f.is_geometry?
		_then
			_continue _endif
		_if not_first?
		_then
			wany(COMMA) _endif
		not_first? << _true

		wany(astr(f.external_name))
		wany(%:)
		wany(astr(_self.perform(f.name)))
	_endloop
	wany(%})
	wany("}")
_endmethod
$

